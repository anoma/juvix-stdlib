module Stdlib.Data.Int.Range;

import Stdlib.Data.Bool open;
import Stdlib.Data.Int open;
import Stdlib.Data.Int.Ord open;

--- A range of integers
type Range :=
  | mkRange : Int → Int → Int → Range;

syntax iterator for {init: 1, range: 1};
{-# specialize: [1, 3] #-}
for : {A : Type} → (A → Int → A) → A → Range → A;
for {A} f a (mkRange low high step) :=
  let
    terminating
    go : A → Int → A;
    go acc n := if (n > high) acc (go (f acc n) (n + step));
  in go a low;

syntax infix 5 to;
to : Int → Int → Range;
to l h := mkRange l h 1;

syntax infix 4 step;
step : Range → Int → Range;
step (mkRange l h _) s := mkRange l h s;
