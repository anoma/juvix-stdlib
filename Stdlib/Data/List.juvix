module Stdlib.Data.List;

import Stdlib.Data.List.Base open public;
import Stdlib.Data.Bool.Base open;
import Stdlib.Data.String.Base open;
import Stdlib.Debug.Fail open;

import Stdlib.Trait.Eq as Eq;
open Eq using {Eq};

import Stdlib.Trait.Ord as Ord;
open Ord using {Ord};

import Stdlib.Trait.Show as Show;
open Show using {Show};

--- ð’ª(1). Partial function that returns the first element of a ;List;.
head : {A : Type} â†’ List A â†’ A
  | (x :: _) := x
  | nil := fail "head: empty list";

module ListTraits;
  Eq : {A : Type} -> Eq.Eq A -> Eq.Eq (List A)
    | {A} (Eq.mkEq eq-a) :=
      let
        go : List A -> List A -> Bool
          | nil nil := true
          | nil _ := false
          | _ nil := false
          | (x :: xs) (y :: ys) := if (eq-a x y) (go xs ys) false;
      in Eq.mkEq go;

  Ord : {A : Type} -> Ord.Ord A -> Ord.Ord (List A)
    | {A} (Ord.mkOrd cmp-a) :=
      let
        go : List A -> List A -> Ord.Ordering
          | nil nil := Ord.EQ
          | nil _ := Ord.LT
          | _ nil := Ord.GT
          | (x :: xs) (y :: ys) :=
            case cmp-a x y
              | Ord.LT := Ord.LT
              | Ord.GT := Ord.GT
              | Ord.EQ := go xs ys;
      in Ord.mkOrd go;

  Show : {A : Type} -> Show.Show A -> Show.Show (List A)
    | {A} (Show.mkShow to-str) :=
      let
        go : List A -> String
          | nil := "nil"
          | (x :: xs) := to-str x ++str " :: " ++str go xs;
      in Show.mkShow
        Î» {
          | nil := "nil"
          | s := "(" ++str go s ++str ")"
        };
end;
