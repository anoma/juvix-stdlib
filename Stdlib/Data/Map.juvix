module Stdlib.Data.Map;

import Stdlib.Data.Pair open;
import Stdlib.Data.Maybe open;
import Stdlib.Data.List open;
import Stdlib.Data.Nat open;
import Stdlib.Data.Bool open;

import Stdlib.Trait.Functor open;
import Stdlib.Trait.Foldable open hiding {foldr; foldl};
import Stdlib.Trait.Ord open;

import Stdlib.Function open;

import Stdlib.Data.Set as Set open using {Set};
import Stdlib.Data.Set.AVL as AVL open using {AVLTree};

import Stdlib.Data.BinaryTree as Tree;

type Binding Key Value :=
  mkBinding@{
    key : Key;
    value : Value
  };

key {Key Value} (binding : Binding Key Value) : Key := Binding.key binding;

value {Key Value} (binding : Binding Key Value) : Value := Binding.value binding;

toPair {Key Value} (binding : Binding Key Value) : Pair Key Value := key binding, value binding;

instance
bindingKeyOrdering {Key Value} {{Ord Key}} : Ord (Binding Key Value) :=
  mkOrd@{
    {-# inline: true #-}
    cmp (b1 b2 : Binding Key Value) : Ordering := Ord.cmp (key b1) (key b2)
  };

type Map Key Value := mkMap (AVLTree (Binding Key Value));

empty {Key Value} : Map Key Value := mkMap AVL.empty;

{-# specialize: [1, f] #-}
insertWith
  {Key Value}
  {{Ord Key}}
  (fun : Value -> Value -> Value)
  (key : Key)
  (value : Value)
  (map : Map Key Value)
  : Map Key Value :=
  case map of
    mkMap tree :=
      let
        fun' (binding1 binding2 : Binding Key Value) : Binding Key Value :=
          case binding1, binding2 of mkBinding k b1, mkBinding _ b2 := mkBinding k (fun b1 b2);
        binding := mkBinding key value;
      in mkMap (Set.insertWith fun' binding tree);

--- ð’ª(log ð“ƒ). Inserts an element into a ;Map; at a given key.
{-# inline: true #-}
insert {Key Value : Type} {{Ord Key}} : Key -> Value -> Map Key Value -> Map Key Value :=
  insertWith \ {old new := new};

--- ð’ª(log ð“ƒ). Queries whether a given key is in a ;Map;.
{-# specialize: [1] #-}
lookup {Key Value} {{Ord Key}} (lookupKey : Key) (container : Map Key Value) : Maybe Value :=
  case container of mkMap s := map value (Set.lookupWith key lookupKey s);

--- ð’ª(log ð“ƒ). Queries whether a given key is in a ;Map;.
{-# specialize: [1] #-}
isMember {Key Value} {{Ord Key}} (key : Key) (container : Map Key Value) : Bool :=
  isJust (lookup key container);

{-# specialize: [1, f] #-}
fromListWithKey
  {Key Value}
  {{Ord Key}}
  (f : Key -> Value -> Value -> Value)
  (xs : List (Pair Key Value))
  : Map Key Value := for (acc := empty) (k, v in xs) {insertWith (f k) k v acc};

{-# inline: true #-}
fromListWith
  {Key Value}
  {{Ord Key}}
  (f : Value -> Value -> Value)
  (xs : List (Pair Key Value))
  : Map Key Value := fromListWithKey (const f) xs;

{-# inline: true #-}
fromList {Key Value} {{Ord Key}} : List (Pair Key Value) -> Map Key Value :=
  fromListWith Î» {old new := new};

toList {Key Value} (container : Map Key Value) : List (Pair Key Value) :=
  case container of mkMap s := map (x in Set.toList s) {toPair x};

{-# specialize: [1, f] #-}
fromSet {Key Value} {{Ord Key}} (f : Key -> Value) (set : Set Key) : Map Key Value :=
  for (acc := empty) (k in set) {insert k (f k) acc};

--- ð’ª(1). Checks if a ;Map; is empty.
{-# inline: true #-}
isEmpty {Key Value} (container : Map Key Value) : Bool :=
  case container of mkMap s := Set.isEmpty s;

--- ð’ª(ð“ƒ). Returns the number of elements of a ;Map;.
{-# inline: true #-}
size {Key Value} (container : Map Key Value) : Nat := case container of mkMap s := Set.size s;

--- ð’ª(log ð“ƒ). Removes a key assignment from a ;Map;.
{-# inline: true #-}
delete {Key Value} {{Ord Key}} (deleteKey : Key) (container : Map Key Value) : Map Key Value :=
  case container of mkMap s := mkMap (Set.deleteWith key deleteKey s);

keys {Key Value} (container : Map Key Value) : List Key :=
  case container of mkMap s := map (x in Set.toList s) {key x};

values {Key Value} (container : Map Key Value) : List Value :=
  case container of mkMap s := map (x in Set.toList s) {value x};

keySet {Key Value} {{Ord Key}} (container : Map Key Value) : Set Key :=
  case container of mkMap s := Set.map (x in s) {key x};

valueSet {Key Value} {{Ord Value}} (container : Map Key Value) : Set Value :=
  case container of mkMap s := Set.map (x in s) {value x};

{-# specialize: [f] #-}
mapValuesWithKey
  {Key Value1 Value2} (f : Key -> Value1 -> Value2) (container : Map Key Value1) : Map Key Value2 :=
  case container of
    mkMap s := mkMap (Set.Internal.unsafeMap \ {(mkBinding k v) := mkBinding k (f k v)} s);

{-# inline: true #-}
mapValues
  {Key Value1 Value2} (f : Value1 -> Value2) (container : Map Key Value1) : Map Key Value2 :=
  mapValuesWithKey (const f) container;

{-# inline: true #-}
singleton {Key Value} {{Ord Key}} (key : Key) (value : Value) : Map Key Value :=
  insert key value empty;

{-# inline: true #-}
foldr
  {Key Value Acc} (f : Key -> Value -> Acc -> Acc) (acc : Acc) (container : Map Key Value) : Acc :=
  case container of mkMap s := rfor (acc := acc) (x in s) {f (key x) (value x) acc};

{-# inline: true #-}
foldl
  {Key Value Acc} (f : Acc -> Key -> Value -> Acc) (acc : Acc) (container : Map Key Value) : Acc :=
  case container of mkMap s := for (acc := acc) (x in s) {f acc (key x) (value x)};

{-# specialize: true, inline: case #-}
instance
functorMapI {Key} : Functor (Map Key) :=
  mkFunctor@{
    map {A B} (f : A -> B) (container : Map Key A) : Map Key B := mapValues f container
  };

{-# specialize: true, inline: case #-}
instance
foldableMapI {Key Value} : Foldable (Map Key Value) (Pair Key Value) :=
  mkFoldable@{
    {-# inline: true #-}
    for {B} (f : B -> Pair Key Value -> B) (acc : B) (container : Map Key Value) : B :=
      foldl \ {a k v := f a (k, v)} acc container;

    {-# inline: true #-}
    rfor {B} (f : B -> Pair Key Value -> B) (acc : B) (container : Map Key Value) : B :=
      foldr \ {k v a := f a (k, v)} acc container
  };

instance
eqMapI {A B} {{Eq A}} {{Eq B}} : Eq (Map A B) := mkEq (Eq.eq on toList);

instance
ordMapI {A B} {{Ord A}} {{Ord B}} : Ord (Map A B) := mkOrd (Ord.cmp on toList);
