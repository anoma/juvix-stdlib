--- AVL trees are a type of self-balancing binary search tree, where the heights
--- of the left and right subtrees of every node differ by at most 1. This
--- ensures that the height of the tree is logarithmic in the number of nodes,
--- which guarantees efficient insertion, deletion, and search operations (all
--- are guaranteed to run in ð’ª(log ð“ƒ) time).
---
--- This module defines an AVL tree data type and provides functions for
--- constructing, modifying, and querying AVL trees.
module Stdlib.Data.Set.AVL;

import Stdlib.Data.Tree as Tree open using {Tree; Forest};
import Stdlib.Data.Maybe open;
import Stdlib.Data.Nat open;
import Stdlib.Data.Int open;
import Stdlib.Data.Bool open;
import Stdlib.Data.Pair open;
import Stdlib.Data.List open using {List; nil; ::; filter; ++; catMaybes};
import Stdlib.Data.String open;
import Stdlib.Trait.Foldable open hiding {foldr; foldl};
import Stdlib.Function open;

--- A self-balancing binary search tree.
type AVLTree (A : Type) :=
  | --- An empty AVL tree.
    empty
  | --- A node of an AVL tree.
    node@{
      element : A;
      height : Nat;
      left : AVLTree A;
      right : AVLTree A
    };

--- ð’ª(1) Retrieves the height of an ;AVLTree;. The height is the distance from
--- the root to the deepest child.
height {A} (tree : AVLTree A) : Nat :=
  case tree of
    | empty := 0
    | node@{height} := height;

module Internal;
  --- ð’ª(n). Maps a function over an ;AVLTree;. Does not check if
  --- after mapping the order of the elements is preserved. It is the
  --- responsibility of the caller to ensure that `f` preserves the ordering.
  unsafeMap {A B} (f : A -> B) (tree : AVLTree A) : AVLTree B :=
    let
      go : AVLTree A -> AVLTree B
        | empty := empty
        | node@{element; height; left; right} :=
          -- TODO: we cannot use record creation syntax here because of a bug in the type-checker (https://github.com/anoma/juvix/issues/3112)
            node
            (f element)
            height
            (go left)
            (go right);
    in go tree;

  type BalanceFactor :=
    | --- Left child is higher.
      LeanLeft
    | --- Equal heights of children.
      LeanNone
    | --- Right child is higher.
      LeanRight;

  diffFactor {A} (tree : AVLTree A) : Int :=
    case tree of
      | empty := 0
      | node@{left; right} := intSubNat (height right) (height left);

  {-# inline: true #-}
  balanceFactor' {A} (left right : AVLTree A) : BalanceFactor :=
    let
      h1 := height left;
      h2 := height right;
    in if
      | h1 < h2 := LeanRight
      | h2 < h1 := LeanLeft
      | else := LeanNone;

  --- ð’ª(1). Computes the balance factor, i.e., the height of the right subtree
  --- minus the height of the left subtree.
  balanceFactor {A} (tree : AVLTree A) : BalanceFactor :=
    -- We avoid `diffFactor` here for efficiency.
    case tree of
      | empty := LeanNone
      | node@{left; right} := balanceFactor' left right;

  --- ð’ª(1). Helper function for creating a node.
  mkNode {A} (value : A) (left : AVLTree A) (right : AVLTree A) : AVLTree A :=
    node value (1 + max (height left) (height right)) left right;

  --- ð’ª(1). Left rotation.
  rotateLeft {A} (tree : AVLTree A) : AVLTree A :=
    case tree of
      | node x _ a (node z _ b c) := mkNode z (mkNode x a b) c
      | n := n;

  --- ð’ª(1). Right rotation.
  rotateRight {A} (tree : AVLTree A) : AVLTree A :=
    case tree of
      | node z _ (node y _ x t3) t4 := mkNode y x (mkNode z t3 t4)
      | n := n;

  --- ð’ª(1). Applies local rotations if needed.
  balance {A} (tree : AVLTree A) : AVLTree A :=
    case tree of
      | empty := empty
      | node@{element; height; left; right} :=
        case balanceFactor' left right of
          | LeanRight :=
            case balanceFactor right of {
              | LeanLeft := rotateLeft (mkNode element left (rotateRight right))
              | _ := rotateLeft tree
            }
          | LeanLeft :=
            case balanceFactor left of {
              | LeanRight := rotateRight (mkNode element (rotateLeft left) right)
              | _ := rotateRight tree
            }
          | LeanNone := tree;
end;

open Internal;

--- ð’ª(log ð“ƒ). Lookup a member from the ;AVLTree; using a projection function.
--- Ord A, Ord K and fun must be compatible. i.e cmp_k (fun a1) (fun a2) == cmp_a a1 a2
{-# specialize: [1, 2] #-}
lookupWith {A K} {{order : Ord K}} (fun : A -> K) (elem : K) (tree : AVLTree A) : Maybe A :=
  let
    {-# specialize-by: [order, fun] #-}
    go : AVLTree A -> Maybe A
      | empty := nothing
      | node@{element; left; right} :=
        case Ord.cmp elem (fun element) of
          | LessThan := go left
          | GreaterThan := go right
          | Equal := just element;
  in go tree;

--- ð’ª(log ð“ƒ). Queries whether an element is in an ;AVLTree;.
{-# specialize: [1] #-}
lookup {A} {{Ord A}} (elem : A) (tree : AVLTree A) : Maybe A := lookupWith id elem tree;

--- ð’ª(log ð“ƒ). Queries whether an element is in an ;AVLTree;.
{-# specialize: [1] #-}
isMember {A} {{Ord A}} (elem : A) (tree : AVLTree A) : Bool := isJust (lookupWith id elem tree);

--- ð’ª(log ð“ƒ). Inserts an element elem into and ;AVLTree; using a function to
--- deduplicate entries.
---
--- Assumption: If a1 == a2 then fun a1 a2 == a1 == a2
--- where == comes from Ord a.
{-# specialize: [1, 2] #-}
insertWith {A} {{order : Ord A}} (fun : A -> A -> A) (elem : A) (tree : AVLTree A) : AVLTree A :=
  let
    {-# specialize-by: [order, fun] #-}
    go : AVLTree A -> AVLTree A
      | empty := mkNode elem empty empty
      | node@{element; height; left; right} :=
        case Ord.cmp elem element of
          | LessThan := balance (mkNode element (go left) right)
          | GreaterThan := balance (mkNode element left (go right))
          | Equal := node (fun element elem) height left right;
  in go tree;

--- ð’ª(log ð“ƒ). Inserts an element into an ;AVLTree;.
{-# specialize: [1] #-}
insert {A} {{Ord A}} (elem : A) (tree : AVLTree A) : AVLTree A := insertWith (flip const) elem tree;

--- ð’ª(log ð“ƒ). Removes the minimum element from an ;AVLTree;.
{-# specialize: [1] #-}
deleteMin {A} {{Ord A}} : (tree : AVLTree A) -> Maybe (Pair A (AVLTree A))
  | empty := nothing
  | node@{element; left; right} :=
    case deleteMin left of
      | nothing := just (element, right)
      | just (element', left') := just (element', mkNode element left' right);

--- ð’ª(log ð“ƒ). Removes an element from an ;AVLTree; based on a projected comparison value.
---
--- Assumption Ord A and Ord B are compatible: Given a1 a2, A then (fun a1 == fun a2) == (a1 == a2)
{-# specialize: [1, 2, 3] #-}
deleteWith
  {A B}
  {{orderA : Ord A}}
  {{orderB : Ord B}}
  (fun : A -> B)
  (elem : B)
  (tree : AVLTree A)
  : AVLTree A :=
  let
    {-# specialize-by: [orderA, orderB, fun] #-}
    go : AVLTree A -> AVLTree A
      | empty := empty
      | node@{element; left; right} :=
        case Ord.cmp elem (fun element) of
          | LessThan := balance (mkNode element (go left) right)
          | GreaterThan := balance (mkNode element left (go right))
          | Equal :=
            case left of
              | empty := right
              | _ :=
                case deleteMin right of
                  | nothing := left
                  | just (minRight, right') := balance (mkNode minRight left right');
  in go tree;

--- ð’ª(log ð“ƒ). Removes an element from an ;AVLTree;.
{-# specialize: [1] #-}
delete {A} {{Ord A}} : A -> AVLTree A -> AVLTree A := deleteWith id;

--- ð’ª(log ð“ƒ). Returns the minimum element of the ;AVLTree;.
lookupMin {A} : AVLTree A -> Maybe A
  | empty := nothing
  | (node y _ empty empty) := just y
  | (node _ _ empty r) := lookupMin r
  | (node _ _ l _) := lookupMin l;

--- ð’ª(log ð“ƒ). Returns the maximum element of the ;AVLTree;.
lookupMax {A} : AVLTree A -> Maybe A
  | empty := nothing
  | (node y _ empty empty) := just y
  | (node _ _ l empty) := lookupMax l
  | (node _ _ _ r) := lookupMax r;

--- ð’ª(ð’¹ log ð“ƒ). Deletes elements from an ;AVLTree;.
{-# specialize: [1] #-}
deleteMany {A} {{Ord A}} : List A -> AVLTree A -> AVLTree A := deleteManyWith id;

--- ð’ª(ð’¹ log ð“ƒ). Deletes elements from an ;AVLTree; based on a projected comparison value.
---
--- Assumption: Ord A and Ord B are compatible, i.e., for a1 a2 in A, we have (fun a1 == fun a2) == (a1 == a2)
{-# specialize: [1, 2] #-}
deleteManyWith
  {A B} {{Ord A}} {{Ord B}} (fun : A -> B) (toDelete : List B) (tree : AVLTree A) : AVLTree A :=
  for (acc := tree) (x in toDelete) {deleteWith fun x acc};

--- ð’ª(ð“ƒ). Checks the ;AVLTree; height invariant. I.e. that
--- every two children do not differ on height by more than 1.
isBalanced {A} : (tree : AVLTree A) -> Bool
  | empty := true
  | tree@node@{left; right} := isBalanced left && isBalanced right && abs (diffFactor tree) <= 1;

--- ð’ª(ð“ƒ log ð“ƒ). Create an ;AVLTree; from an unsorted ;List;.
{-# specialize: [1] #-}
fromList {A} {{Ord A}} (list : List A) : AVLTree A := for (acc := empty) (x in list) {insert x acc};

--- ð’ª(1). Checks if an ;AVLTree; is empty.
{-# inline: true #-}
isEmpty {A} (tree : AVLTree A) : Bool :=
  case tree of
    | empty := true
    | node@{} := false;

--- ð’ª(ð“ƒ). Returns the number of elements of an ;AVLTree;.
size {A} : AVLTree A -> Nat
  | empty := 0
  | node@{left; right} := 1 + size left + size right;

{-# specialize: [1] #-}
foldr {A B} (fun : A -> B -> B) (acc : B) : AVLTree A -> B
  | empty := acc
  | node@{element; left; right} := foldr fun (fun element (foldr fun acc right)) left;

{-# specialize: [1] #-}
foldl {A B} (fun : B -> A -> B) (acc : B) : AVLTree A -> B
  | empty := acc
  | node@{element; left; right} := foldl fun (fun (foldl fun acc left) element) right;

{-# specialize: true, inline: case #-}
instance
polymorphicFoldableAVLTreeI : Polymorphic.Foldable AVLTree :=
  Polymorphic.mkFoldable@{
    {-# inline: true #-}
    for {A B} (f : B -> A -> B) (acc : B) (tree : AVLTree A) : B := foldl f acc tree;
    {-# inline: true #-}
    rfor {A B} (f : B -> A -> B) (acc : B) (tree : AVLTree A) : B := foldr (flip f) acc tree
  };

{-# specialize: true, inline: true #-}
instance
foldableAVLTreeI {A} : Foldable (AVLTree A) A := fromPolymorphicFoldable;

--- ð’ª(n). Returns the elements of an ;AVLTree; in ascending order.
toList {A} (tree : AVLTree A) : List A := rfor (acc := nil) (x in tree) {x :: acc};

--- ð’ª(n log n). Returns an ;AVLTree; containing elements that are members of both ;AVLTree;s.
{-# specialize: [1] #-}
intersection {A} {{Ord A}} (tree1 tree2 : AVLTree A) : AVLTree A :=
  for (acc := empty) (x in tree1)
    {if
      | isMember x tree2 := insert x acc
      | else := acc};

--- ð’ª(n log n). Returns an ;AVLTree; containing elements that are members of the first but not the second ;AVLTree;.
{-# specialize: [1] #-}
difference {A} {{Ord A}} (tree1 tree2 : AVLTree A) : AVLTree A :=
  for (acc := empty) (x in tree1)
    {if
      | isMember x tree2 := acc
      | else := insert x acc};

--- ð’ª(n log n). Returns an ;AVLTree; containing elements that are members of either the first or second ;AVLTree;.
{-# specialize: [1] #-}
union {A} {{Ord A}} (tree1 tree2 : AVLTree A) : AVLTree A :=
  for (acc := tree1) (x in tree2) {insert x acc};

syntax iterator all {init := 0; range := 1};
--- ð’ª(ð“ƒ). Returns ;true; if all elements of the ;AVLTree; satisfy the predicate.
{-# specialize: [1] #-}
all {A} (predicate : A -> Bool) (tree : AVLTree A) : Bool :=
  let
    go : AVLTree A -> Bool
      | empty := true
      | node@{element; left; right} := predicate element && go left && go right;
  in go tree;

syntax iterator any {init := 0; range := 1};
--- ð’ª(ð“ƒ). Returns ;true; if some elements of the ;AVLTree; satisfies the predicate.
{-# specialize: [1] #-}
any {A} (predicate : A -> Bool) (tree : AVLTree A) : Bool :=
  let
    go : AVLTree A -> Bool
      | empty := true
      | node@{element; left; right} := predicate element || go left || go right;
  in go tree;

syntax iterator filter {init := 0; range := 1};
--- ð’ª(n log n). Returns an ;AVLTree; containing all elements of the tree that satisfy the predicate.
{-# specialize: [1] #-}
filter {A} {{Ord A}} (predicate : A -> Bool) (tree : AVLTree A) : AVLTree A :=
  for (acc := empty) (x in tree)
    {if
      | predicate x := insert x acc
      | else := acc};

--- O(n log n). Partition the set into two sets, one with all elements that satisfy the predicate and one with all elements that don't satisfy the predicate.
syntax iterator partition {init := 0; range := 1};
partition {A} {{Ord A}} (predicate : A -> Bool) (tree : AVLTree A) : Pair (AVLTree A) (AVLTree A) :=
  for (trueSet, falseSet := empty, empty) (x in tree)
    {if
      | predicate x := insert x trueSet, falseSet
      | else := trueSet, insert x falseSet};

--- ð’ª(1). Creates an ;AVLTree; with a single element.
singleton {A} (element : A) : AVLTree A := mkNode element empty empty;

--- ð’ª(n log n). Checks if all elements of tree1 are in tree2.
isSubset {A} {{Ord A}} (tree1 tree2 : AVLTree A) : Bool := all (x in tree1) {isMember x tree2};

syntax iterator map {init := 0; range := 1};
map {A B} {{Ord B}} (f : A -> B) (tree : AVLTree A) : AVLTree B :=
  for (acc := empty) (x in tree) {insert (f x) acc};

--- Formats the tree in a debug friendly format.
prettyDebug {A} {{Show A}} (tree : AVLTree A) : String :=
  let
    go : AVLTree A -> String
      | empty := "_"
      | tree@node@{element; left; right} :=
        "("
          ++str go left
          ++str " h"
          ++str Show.show (diffFactor tree)
          ++str " "
          ++str Show.show element
          ++str " "
          ++str go right
          ++str ")";
  in go tree;

--- ð’ª(ð“ƒ).
toTree {A} : (tree : AVLTree A) -> Maybe (Tree A)
  | empty := nothing
  | node@{element; left; right} :=
    just (Tree.node element (catMaybes (toTree left :: toTree right :: nil)));

--- Returns the textual representation of an ;AVLTree;.
pretty {A} {{Show A}} (tree : AVLTree A) : String := maybe "empty" Tree.treeToString (toTree tree);

instance
eqAVLTreeI {A} {{Eq A}} : Eq (AVLTree A) := mkEq (Eq.eq on toList);

instance
ordAVLTreeI {A} {{Ord A}} : Ord (AVLTree A) := mkOrd (Ord.cmp on toList);
