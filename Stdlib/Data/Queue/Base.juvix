--- This module provides an implementation of a First-In, First-Out (FIFO)
--- queue based on Okasaki's "Purely Functional Data Structures." Ch.3.
---
--- This Okasaki Queue version data structure ensures amortized constant-time
--- performance for basic operations such as push, pop, and front.
---
--- The Okasaki Queue consists of two lists (front and back) to separate
--- the concerns of adding and removing elements for ensuring efficient
--- performance.
module Stdlib.Data.Queue.Base;

import Stdlib.Data.List open;
import Stdlib.Data.Bool open;
import Stdlib.Data.Maybe open;
import Stdlib.Data.Pair open;
import Stdlib.Data.Nat open;
import Stdlib.Function open;
import Stdlib.Trait.Foldable open;

--- A first-in-first-out data structure
type Queue (A : Type) := queue (List A) (List A);

--- ð’ª(1). The empty ;Queue;.
empty {A} : Queue A := queue nil nil;

--- ð’ª(1). Returns ;true; when the ;Queue; has no elements.
isEmpty {A} : Queue A -> Bool
  | (queue nil nil) := true
  | _ := false;

--- ð’ª(1). Returns first element of the ;Queue;, if any.
head {A} : Queue A -> Maybe A
  | (queue nil _) := nothing
  | (queue (x :: _) _) := just x;

--- ð’ª(1). Removes the first element from the ;Queue;. If the ;Queue; is empty
--  then returns ;nothing;.
tail {A} : Queue A -> Maybe (Queue A)
  | (queue nil _) := nothing
  | (queue (_ :: front) back) := just (queue front back);

--- ð’ª(n) worst-case, but ð’ª(1) amortized
{-# inline: true #-}
check {A} : Queue A -> Queue A
  | (queue nil back) := queue (reverse back) nil
  | q := q;

--- ð’ª(n) worst-case, but ð’ª(1) amortized. Returns the first element and the
--  rest of the ;Queue;. If the ;Queue; is empty then returns ;nothing;.
pop {A} : Queue A -> Maybe (Pair A (Queue A))
  | (queue nil _) := nothing
  | (queue (x :: front) back) := just (x, check (queue front back));

--- ð’ª(1). Adds an element to the end of the ;Queue;.
push {A} (x : A) : Queue A -> Queue A
  | (queue front back) := check (queue front (x :: back));

--- ð’ª(n). Adds a list of elements to the end of the ;Queue;.
pushMany {A} (xs : List A) (q : Queue A) : Queue A := for (acc := q) (x in xs) push x acc;

--- ð’ª(n). Build a ;Queue; from a ;List;.
fromList {A} (xs : List A) : Queue A := pushMany xs empty;

--- toList: O(n). Returns a ;List; of the elements in the ;Queue;.
--- The elements are in the same order as they appear in the ;Queue;
--- (i.e. the first element of the ;Queue; is the first element of the ;List;).
toList {A} : Queue A -> List A
  | (queue front back) := front ++ reverse back;

--- ð’ª(n). Returns the number of elements in the ;Queue;.
size {A} : Queue A -> Nat
  | (queue front back) := length front + length back;

instance
eqQueueI {A} {{Eq A}} : Eq (Queue A) := mkEq ((==) on toList);

instance
showQueueI {A} {{Show A}} : Show (Queue A) := mkShow (toList >> Show.show);

instance
ordQueueI {A} {{Ord A}} : Ord (Queue A) := mkOrd (Ord.cmp on toList);
