module Stdlib.Trait.Foldable.Polymorphic;

import Stdlib.Function open;
import Juvix.Builtin.V1.Nat.Base open;
import Stdlib.Data.Pair.Base open;

--- A trait for combining elements into a single result, processing one element at a time.
trait
type Foldable (F : Type -> Type) :=
  mkFoldable@{
    syntax iterator for {init := 1; range := 1};
    for : {A B : Type} -> (B -> A -> B) -> B -> F A -> B;

    syntax iterator rfor {init := 1; range := 1};
    rfor : {A B : Type} -> (B -> A -> B) -> B -> F A -> B;
  };

open Foldable public;

foldli
  {F : Type -> Type}
  {{Foldable F}}
  {Elem Acc : Type}
  (fun : Nat -> Acc -> Elem -> Acc)
  (initialValue : Acc)
  (container : F Elem)
  : Acc :=
  let
    g := Î»{(index, acc) ele := suc index, fun index acc ele};
  in for (acc := zero, initialValue) (x in container) {
      g acc x
    }
    |> snd;

--- Combine the elements of the type using the provided function starting with the element in the left-most position.
{-# inline: true #-}
foldl
  {F : Type -> Type}
  {{Foldable F}}
  {Elem Acc : Type}
  (fun : Acgc -> Elem -> Acc)
  (initialValue : Acc)
  (container : F Elem)
  : Acc :=
  for (acc := initialValue) (x in container) {
    fun acc x
  };

--- Combine the elements of the type using the provided function starting with the element in the right-most position.
{-# inline: true #-}
foldr
  {F : Type -> Type}
  {{Foldable F}}
  {Elem Acc : Type}
  (fun : Elem -> Acc -> Acc)
  (initialValue : Acc)
  (container : F Elem)
  : Acc :=
  rfor (acc := initialValue) (x in container) {
    fun x acc
  };
